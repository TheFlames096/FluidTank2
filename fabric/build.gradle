import com.kotori316.plugin.cf.CallVersionFunctionTask

import java.util.stream.Collectors

plugins {
    id("my.common")
    id("architectury-plugin").version("3.4-SNAPSHOT")
    id("dev.architectury.loom").version("1.3-SNAPSHOT")
    id("com.github.johnrengelman.shadow").version("8.1.1")
    id("com.matthewprenger.cursegradle").version("1.4.0")
    id("com.modrinth.minotaur").version("2.+")
    id("com.kotori316.plugin.cf").version("1.+")
}

archivesBaseName = "${project.archives_base_name}-fabric"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    runs {
        client {
            configName = "Fabric Client"
            runDir = "run"
            source sourceSets.test
        }
        server {
            configName = "Fabric Server"
            runDir = "run-server"
        }

        gameTest {
            name "Fabric GameTest"
            server()
            vmArg "-ea"
            //noinspection SpellCheckingInspection
            property("fabric-api.gametest")
            //noinspection SpellCheckingInspection
            property("fabric-api.gametest.report-file", "game-test/test-results/game_test.xml")
            runDir = "game-test"
            source sourceSets.test
        }
    }
}

repositories {

}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    testCompileClasspath.extendsFrom(compileClasspath)
    testRuntimeClasspath.extendsFrom(runtimeClasspath)
}

dependencies {
    modImplementation(group: "net.fabricmc", name: "fabric-loader", version: project.fabric_loader_version)
    modApi(group: "net.fabricmc.fabric-api", name: "fabric-api", version: project.fabric_api_version)

    modRuntimeOnly(group: "com.kotori316", name: "scalable-cats-force-fabric", version: project.slp_fabric_version, classifier: "dev") { transitive false }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    // Other mods
    modImplementation(group: "curse.maven", name: "jade-324717", version: project.jade_fabric_id)
    modImplementation(group: "mezz.jei", name: "jei-1.20.1-fabric", version: project.jei_fabric_version)
    modCompileOnly(group: "appeng", name: "appliedenergistics2-fabric", version: project.ae2_fabric_version) { transitive false }
    //noinspection SpellCheckingInspection
    modImplementation(group: "teamreborn", name: "energy", version: "3.0.0")

    testImplementation "net.fabricmc:fabric-loader-junit:${project.fabric_loader_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev")
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev-only-platform")
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

final boolean RELEASE_DEBUG = Boolean.parseBoolean(System.getenv("RELEASE_DEBUG") ?: "true")

String createChangelog(boolean useLongFormat) {
    String base = """\
        # Large Fluid Tank for fabric
        
        | Dependency | Version |
        | -- | -- |
        | Minecraft | ${project.minecraft_version} |
        | Fabric | ${project.fabric_api_version} |
        | Fabric Loader | ${project.fabric_loader_version} |
        | scalable-cats-force | ${project.slp_fabric_version} |
        | Applied Energistics 2 | ${project.ae2_fabric_version} |
        | Jade | File id: ${project.jade_fabric_id} |
        """.stripIndent()
    String fromFile = rootProject.file(project.changelog_file).text
    if (useLongFormat) {
        return base + System.lineSeparator() + fromFile
    } else {
        String shortFormat = fromFile.split("---", 2)[0]
        return base + System.lineSeparator() + shortFormat
    }
}

String noHeaderChangelog() {
    String fromFile = rootProject.file(project.changelog_file).text
    String headVersion = fromFile.split("---", 2)[0]
            .lines().filter { s -> !s.startsWith("## ") }.collect(Collectors.joining(System.lineSeparator()))
            .strip()
    return headVersion;
}

tasks.register("checkChangelog") {
    doLast {
        println(createChangelog(true))
        println("-" * 20 + " noHeader " + "-" * 20)
        println(noHeaderChangelog())
    }
}

publishing {
    publications {
        create("mavenFabric", MavenPublication.class) {
            artifactId = project.maven_base_name + "-" + project.name + "-" + project.minecraft_version
            from(components["java"])
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        if (!Boolean.parseBoolean(System.getenv("CI")) || !RELEASE_DEBUG) {
            maven {
                name = "AzureRepository"
                url = uri("https://pkgs.dev.azure.com/Kotori316/minecraft/_packaging/mods/maven/v1")
                credentials {
                    username = project.findProperty("azureUserName") ?: System.getenv("AZURE_USER_NAME") ?: ""
                    password = project.findProperty("azureToken") ?: System.getenv("AZURE_TOKEN") ?: "TOKEN"
                }
            }
        }
    }
}

curseforge {
    apiKey = project.findProperty("curseforge_additional-enchanted-miner_key") ?: System.getenv("CURSE_TOKEN") ?: ""
    project {
        id = "411564"
        changelogType = "markdown"
        changelog = createChangelog(true)
        releaseType = "release"
        addGameVersion("Fabric")
        addGameVersion(project.minecraft_version)
        mainArtifact(tasks.remapJar) {
            displayName = "${project.version}-fabric"
        }
        addArtifact(tasks.shadowJar)
        addArtifact(tasks.sourcesJar)
        relations {
            requiredDependency "scalable-cats-force"
        }
    }
    options {
        curseGradleOptions.debug = RELEASE_DEBUG // defaults to false
        javaVersionAutoDetect = false
        forgeGradleIntegration = false
    }
}

modrinth {
    token.set((project.findProperty("modrinthToken") ?: System.getenv("MODRINTH_TOKEN") ?: "") as String)
    projectId = "large-fluid-tank"
    versionType = "release"
    versionName = "${project.version}-fabric"
    uploadFile = tasks.remapJar
    getAdditionalFiles().set([
            tasks.shadowJar,
            tasks.sourcesJar,
    ])
    getGameVersions().set([project.minecraft_version])
    getLoaders().set(["fabric"])
    changelog = createChangelog(false)
    debugMode = RELEASE_DEBUG
    dependencies {
        required.project("scalable-cats-force")
    }
}

remapJar.finalizedBy("jksSignJar")

tasks.register("jksSignJar") {
    dependsOn("remapJar")
    boolean executeCondition = project.hasProperty("jarSign.keyAlias") &&
            project.hasProperty("jarSign.keyLocation") &&
            project.hasProperty("jarSign.storePass")
    onlyIf { executeCondition }
    doLast {
        //noinspection HttpUrlsUsage
        ant.signjar(
                jar: remapJar.archiveFile.get(),
                alias: project.findProperty("jarSign.keyAlias") ?: "",
                keystore: project.findProperty("jarSign.keyLocation") ?: "",
                storepass: project.findProperty("jarSign.storePass") ?: "",
                sigalg: "Ed25519",
                digestalg: "SHA-256",
                tsaurl: "http://timestamp.digicert.com",
        )
    }
}

signing {
    sign(publishing.publications)
    sign(tasks.shadowJar, tasks.remapJar, tasks.sourcesJar)
}

boolean hasGpgSignature = project.hasProperty("signing.keyId") &&
        project.hasProperty("signing.password") &&
        project.hasProperty("signing.secretKeyRingFile")

tasks.withType(Sign).configureEach {
    it.onlyIf {
        hasGpgSignature
    }
}

tasks.withType(AbstractPublishToMaven).configureEach {
    if (hasGpgSignature) {
        it.dependsOn(":fabric:signRemapJar")
    }
}

tasks.register("registerVersion", CallVersionFunctionTask.class) {
    functionEndpoint = readVersionFunctionEndpoint(project)
    gameVersion = project.findProperty("minecraft_version")
    platform = "fabric"
    modName = project.findProperty("maven_base_name")
    changelog = noHeaderChangelog()
}
